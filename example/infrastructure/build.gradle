plugins {
    id 'java'
    id 'application'
    id 'io.gatling.gradle' version '3.11.5'
}

repositories {
    mavenCentral()
}


test {
    useJUnitPlatform()
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir 'src/integrationTest/java'
        }
        resources.srcDir 'src/integrationTest/resources'
    }

    gatling {
        java {
            srcDir 'src/loadTest/java'
        }
        resources.srcDir 'src/loadTest/resources'
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.13'
    implementation 'ch.qos.logback:logback-core:1.5.6'
    implementation 'ch.qos.logback:logback-classic:1.5.6'
    implementation 'com.linecorp.armeria:armeria:1.29.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.12.0'
    testImplementation 'org.testcontainers:testcontainers:1.19.8'

    gatling project(':domain')
    implementation project(':domain')
}

tasks.register('integrationTest', Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}